# Maintainer: graysky <graysky AT archlinux DOT org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Austin ( doorknob60 [at] gmail [dot] com )

pkgname=broadcom-wl-uksm-ck
pkgver=5.100.82.112
pkgrel=2
_pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver for linux-ck.'
_extramodules=extramodules-3.6-uksm-ck
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgdesc="${_pkgdesc}"
arch=('i686' 'x86_64')
url='http://www.broadcom.com/support/802.11/linux_sta.php'
license=('custom')
depends=('linux-uksm-ck>=3.6' 'linux-uksm-ck<3.7')
makedepends=('linux-uksm-ck-headers>=3.6' 'linux-uksm-ck-headers<3.7')
#replaces=()
#conflicts=()
#groups=('ck-generic')

[[ $CARCH = x86_64 ]] && ARCH=x86_64 || ARCH=x86_32
source=("hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz"
'modprobe.d'
'license.patch'
'user-ioctl.patch'
'linux-recent.patch')
sha1sums=('5bd78c20324e6a4aa9f3fafdc6f0155e884d5131'
          '89bf92286ede30dd85304c6c4e42e89cfdc0f60a'
          'ea7b67982ddc0f56fd3becb9914fd4458fe7d373'
          'e0366dd15ea5b25aae0f61bec6be7a203dfca7e4'
          'df3cd65e24beed349a40abcf476bdaa82bbc57d3')
[[ $CARCH = x86_64 ]] && sha1sums[0]='5bd78c20324e6a4aa9f3fafdc6f0155e884d5131'

install=broadcom-wl-ck.install

build() {
	cd "${srcdir}"
	patch -p1 -i linux-recent.patch
	patch -p1 -i user-ioctl.patch
	patch -p1 -i license.patch

	make -C /usr/lib/modules/"${_kernver}"/build M=`pwd`
}

package() {
	cd "${srcdir}"

	install -D -m 755 wl.ko "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"
}
