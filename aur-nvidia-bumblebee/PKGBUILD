# $Id$
# Maintainer : Samsagax <samsagax@gmail.com>
# Contributor : Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-bumblebee
pkgver=295.71
_extramodules=extramodules-3.5-ck
pkgrel=1
pkgdesc="NVIDIA drivers for linux. Packaged for Bumblebee"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-ck-corex>=3.5' 'linux-ck-corex<3.6' "nvidia-utils-bumblebee=${pkgver}")
provides=("nvidia=${pkgver}")
makedepends=('linux-ck-headers>=3.5' 'linux-ck-headers<3.6')
conflicts=('nvidia' 'nvidia-96xx' 'nvidia-173xx' 'dkms-nvidia')
license=('custom')
install=nvidia.install
options=(!strip)

_arch='x86_64'
_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
source=("NVIDIA-Linux-x86_64-295.71-no-compat32.run")
md5sums=('820a6fc65e0eecf8e6f3ae7a0b96fc4f')


build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version || true)"
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
